apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def sdkVersionCode = 5
def sdkVersionName = "1.0.4"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode sdkVersionCode
        versionName sdkVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'

            def travisBuild = System.getenv("TRAVIS") == "true"
            buildConfigField "boolean", "TRAVIS", travisBuild.toString()
        }
        debug {
            def travisBuild = System.getenv("TRAVIS") == "true"
            buildConfigField "boolean", "TRAVIS", travisBuild.toString()
        }
    }
    testOptions {
        unitTests.returnDefaultValues= true
    }

    lintOptions {
        abortOnError false
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.squareup.okhttp3:okhttp:3.7.0')

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.21"
}

bintray {
    user = hasProperty('bintray_user') ? bintray_user : System.getenv('bintray_user')
    key = hasProperty('bintray_apikey') ? bintray_apikey : System.getenv('bintray_apikey')
    publications = ['GiphyPublication']
    pkg {
        repo = 'giphy-sdk'
        name = 'core'
        userOrg = 'giphy'
        licenses = ['MPL-2.0']
        vcsUrl = 'https://github.com/Giphy/giphy-android-sdk-core'
        version {
            name = sdkVersionName
            desc = 'Giphy Core SDK'
            released  = new Date()
            vcsTag = sdkVersionName
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "Mozilla Public License Version 2.0"
            url "https://www.mozilla.org/en-US/MPL/2.0/"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "bogdantirca92"
            name "Bogdan Tirca"
            email "bogdan@giphy.com"
        }
    }

    scm {
        url "https://github.com/giphy/giphy-android-sdk-core"
    }
}

publishing {
    publications {
        GiphyPublication(MavenPublication) {
            groupId 'com.giphy.sdk'
            artifactId 'core'
            version sdkVersionName

            artifact("$buildDir/outputs/aar/app-release.aar")

            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if(it.group) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                root.appendNode('description', 'The Giphy Core SDK is a wrapper around Giphy API.')
                root.appendNode('name', 'Giphy Core SDK')
                root.appendNode('url', 'https://developers.giphy.com/docs/')
                root.children().last() + pomConfig
            }
        }
    }
}
repositories {
    mavenCentral()
}
